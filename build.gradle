buildscript {
  ext {
    // Since this plugin implements itself, we must declare our compile dependencies on the classpath.
    // Define them here so we don't write them twice.
    compileDependencies = [
        'com.episode6.hackit.chop:chop-core:0.1.8',
        'com.episode6.hackit.chop:chop-groovy:0.1.8']
  }
  repositories {
    jcenter()
  }
  dependencies {
    classpath 'com.episode6.hackit.deployable:deployable:0.1.2'
    classpath compileDependencies
  }
}

group = "com.episode6.hackit.gdmc"
version = "0.1.1"

repositories {
  jcenter()
}

apply plugin: 'groovy'
apply plugin: 'java-gradle-plugin'
apply plugin: 'com.episode6.hackit.deployable.jar'
apply plugin: 'com.episode6.hackit.deployable.addon.groovydocs'

GroovyScriptEngine scriptEngine = new GroovyScriptEngine([
    file('src/main/groovy').absolutePath,
    file('src/main/resources').absolutePath
].toArray(new String[2]),
    this.class.classLoader)
apply plugin: scriptEngine.loadScriptByName('com/episode6/hackit/gdmc/plugin/GdmcPlugin.groovy')

configurations {
  // A place for dependencies that must be placed in the pluginClasspath for testing
  // but do not need to be compile dependencies. (i.e. this is for plugins we need to
  // test for compatibility with)
  alsoUnderTest
}

dependencies {
  compile localGroovy()
  compile gradleApi()

  compile compileDependencies

  testCompile("org.spockframework:spock-core")  {
    exclude module: 'groovy-all'
  }
  testCompile gradleTestKit()
  testCompile 'com.episode6.hackit.groovykit:gk-files'
  testCompile 'com.episode6.hackit.groovykit:gk-versions'

  alsoUnderTest 'com.episode6.hackit.deployable:deployable'
  alsoUnderTest 'com.android.tools.build:gradle'
}

// Add our alsoUnderTest dependencies to GradleRunner's plugin classpath
pluginUnderTestMetadata {
  pluginClasspath = pluginClasspath.plus(configurations.alsoUnderTest)
}

// Ensure maven consumers are aware of our alsoUnderTest dependencies
conf2ScopeMappings.addMapping(0, configurations.alsoUnderTest, "test")

test {
  testLogging {
    showStandardStreams = true
  }
}
